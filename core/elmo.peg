package elmo

type ElmoGrammar Peg {

}

Script <- Spacing (Line)* EOT

Line <- (NewLine)? Argument (COLON)? (Argument)? ((COMMA (NewLine)?)? Argument)* (PipedOutput / EndOfLine)?

PipedOutput <- PIPE Line

EndOfLine <- PCOMMA / NewLine

Argument <- Identifier (DOT Identifier)*
           / StringLiteral
           / LongStringLiteral
           / Number
           / FunctionCall
           / Block
           / List

FunctionCall <- (LPAR Line RPAR) / ((DOLLAR/AMPERSAND) Argument (DOT Argument)*)

Block <- LCURLY (NewLine)* (Line)* RCURLY

BlockWithoutSpacing <- LCURLY (NewLine)* (Line)* '}'

List <- LBRACKET (NewLine)* (Argument/NewLine)? ((COMMA (NewLine)?)? Argument/NewLine)* RBRACKET


# SPACING

Spacing
   <- ( WhiteSpace
      / LongComment
      / LineComment
      )*

WhiteSpace  <- [ \t]
LongComment <- '/*' (!'*/'.)* '*/'
LineComment <- '#' (!'\n' .)*

# NEW LINE

NewLine <- ([\n\r] Spacing)+

#
# IDENTIFIERS
#

Identifier <- IdNondigit IdChar* (IdEnd? Spacing / IdEnd Spacing?) / IdEnd Spacing

IdNondigit <- [a-z] / [A-Z] / '_'

IdChar <- [a-z] / [A-Z] / [0-9] / '_'

IdEnd <- [?!]

#
# STRING LITERALS
#

StringLiteral <- Quote StringChar* Quote Spacing
Quote <- ["]
StringChar <- Escape / ![\"\n\\] .
Escape <- '\\' (BlockWithoutSpacing / .)

# Backticked string: `...` or `...``...` or `...`{}...`
#
LongStringLiteral <- BackTick LongStringChar* BackTick Spacing
BackTick <- [`]
LongStringChar <- LongEscape / (!'`'.)
LongEscape <- '`' (BlockWithoutSpacing / '`')

#
# NUMBERS
#

Number <- Numbers Spacing
Numbers <- ('-')? [0-9][0-9]* ('.' [0-9][0-9]*)?

#
# PUNCTUATORS
#

LPAR      <-  '('         Spacing
RPAR      <-  ')'         Spacing
LCURLY    <-  '{'         Spacing
RCURLY    <-  '}'         Spacing
LBRACKET  <-  '['         Spacing
RBRACKET  <-  ']'         Spacing
COMMA     <-  ','         Spacing
PCOMMA    <-  ';'         Spacing
COLON     <-  ':'         Spacing
DOT       <-  '.'         Spacing
PIPE      <-  '|'         Spacing
DOLLAR    <-  '$'         Spacing
AMPERSAND <-  '&'         Spacing
EOT       <-  !.
